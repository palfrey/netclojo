S = (block endline)* block whitespace? comment? newline?
<block> = line | whitespace?
line = whitespace? (if| ifelse | (identifier (whitespace expression)*)) finish
<finish> = whitespace? comment?
<newline> = <"\n">
end = <"end">
to = <"to"> whitespace identifier whitespace list
identifier = #'[a-zA-Z][a-zA-Z-\d\?]*'
<comment> = <";"> <#'[^\n]*'>
<whitespace> = <#'[ \t]+'>
number = #"-?\d+(?:\.\d+)?"
report = <"report"> whitespace expression
<expression> = number
	| identifier
	| (expression whitespace expression)
	| (expression whitespace binary whitespace expression)
	| brackets
	| unary whitespace expression
	| list
	| string
	| item
item = <"?"> #"\d+"?
string = <"\""> #'[^\"]*' <"\"">
<expressions> = expression? (whitespace expression)*
<statements> = spacing? line (newline line)* spacing
list = <"["> (expressions | statements) <"]">
brackets = <"("> whitespace? expression whitespace? <")">
<unary> = minus
<binary> = gte | gt | multiply | add | minus | equal
gte = <">=">
gt = <">">
minus = <"-">
multiply = <"*">
add = <"+">
equal = <"=">
<endline> = finish newline whitespace?
<spacing> = (whitespace | endline)
ask = <"ask"> whitespace identifier spacing (number spacing)* <"["> statements <"]">
ifblock = <"["> statements <"]">
if = <"if"> whitespace expression finish newline whitespace? ifblock
ifelse = <"ifelse"> whitespace expression spacing ifblock spacing ifblock
